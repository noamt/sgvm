buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.3'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'artifactory-publish'

group = 'org.10ne'
version = '0.0.1.x-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile('net.gvmtool:gvm-sdk:0.5.5')
    compile('org.codehaus.groovy:groovy-all:2.2.1')
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0')
    testCompile('cglib:cglib-nodep:3.1')
    testCompile('org.objenesis:objenesis:2.1')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allGroovy
}

jar.dependsOn sourceJar

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repoKey = 'libs-release'
    }
    publish {
        defaults {
            publishArtifacts = true
        }
    }
}

publishing {
    publications {
        sgvm(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.sgvm)
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
        wildcards += '?*.gradle;?*.js'
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                // Use git
                def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
                vcsConfig.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

task wrapper(type: Wrapper, description: 'Gradle Wrapper task') {
    gradleVersion = '1.11'
}